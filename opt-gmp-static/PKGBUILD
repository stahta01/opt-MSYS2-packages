# Contributor: Alexey Pavlov <Alexpux@gmail.com>

_install_prefix="/opt${MSYSTEM_PREFIX}"

_enable_gcc_bootmode=yes # no

_realname='gmp'
pkgbase=opt-gmp-static
pkgname=('opt-gmp-static')
pkgver=6.3.0
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
groups=('libraries' 'development')
arch=('i686' 'x86_64')
options=('staticlibs' '!strip')
url="https://gmplib.org/"
msys2_references=(
  "cpe: cpe:/a:gmplib:gmp"
)
license=('LGPL3')
makedepends=('autotools')
if [[ "$_enable_gcc_bootmode" == "yes" ]]; then
  makedepends+=('gcc')
else
  makedepends+=('opt-gcc')
fi
source=(https://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.xz{,.sig}
        gmp-gcc-15.patch)
sha256sums=('a3c2b80201b89e68616f4ad30bc66aee4927c3ce50e33929ca819d5c43538898'
            'SKIP'
            'fd0f1b48dbe72413efbd9c45f1ca7013d555f46c578446ce3ba202e3a9b96c6c')
validpgpkeys=('5972C48224C1E7E62AA0275D1150EE33C1A000B0'   # Marco Bodrato
              '343C2FF0FBEE5EC2EDBEF399F3599FF828C67298')   # Niels MÃ¶ller

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Nbp1 -i "${srcdir}/gmp-gcc-15.patch" # Fix build with GCC 15gmp-gcc-15.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  if [[ "$_enable_gcc_bootmode" != "yes" ]]; then
    export PATH="/opt/usr/bin:$PATH"
    export CC=/opt/usr/bin/gcc.exe
    export CXX=/opt/usr/bin/g++.exe
    export RANLIB=/opt/usr/bin/ranlib.exe
    export LD=/opt/usr/bin/ld.exe
  fi


# get rid of those pesky declspec warnings
  CFLAGS+=" -Wno-attributes" \
  CXXFLAGS+=" -Wno-attributes" \
  ./configure --build=${CHOST} \
    --prefix=${_install_prefix} \
    --enable-cxx \
    --enable-fat \
    --disable-shared --enable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package_opt-gmp-static() {
  mkdir -p ${pkgdir}${_install_prefix}
  cp -rf $srcdir/dest${_install_prefix}/include ${pkgdir}${_install_prefix}/
  cp -rf $srcdir/dest${_install_prefix}/lib ${pkgdir}${_install_prefix}/
  cp -rf $srcdir/dest${_install_prefix}/share ${pkgdir}${_install_prefix}/
}
