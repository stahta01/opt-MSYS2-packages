# Contributor: Alexey Pavlov <Alexpux@gmail.com>

_install_prefix="/opt${MSYSTEM_PREFIX}"

_enable_gcc_bootmode=yes

_realname='mpc'
pkgbase=opt-${_realname}-static
pkgname=('opt-mpc-static')
pkgver=1.3.1
pkgrel=1
pkgdesc='Multiple precision complex arithmetic library'
groups=('libraries' 'development')
url='http://www.multiprecision.org'
license=('LGPL')
arch=('i686' 'x86_64')
makedepends=('opt-gmp-static' 'opt-mpfr-static' 'autotools' 'gcc')
options=('!emptydirs' 'staticlibs')
source=(https://ftp.gnu.org/gnu/mpc/mpc-${pkgver}.tar.gz{,.sig})
sha256sums=('ab642492f5cf882b74aa0cb730cd410a81edcdbec895183ce930e706c1c759b8'
            'SKIP')
validpgpkeys=('AD17A21EF8AED8F1CC02DBD9F7D5C9BF765C61E3')  # Andreas Enge
msys2_references=(
  'archlinux: libmpc'
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  autoreconf -fiv
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  if [[ "$_enable_gcc_bootmode" != "yes" ]]; then
    export PATH="/opt/usr/bin:$PATH"
    export CC=/opt/usr/bin/gcc.exe
    export CXX=/opt/usr/bin/g++.exe
    export RANLIB=/opt/usr/bin/ranlib.exe
    export LD=/opt/usr/bin/ld.exe
  fi

  ./configure \
    --build="${CHOST}" \
    --prefix=${_install_prefix} \
    --with-gmp=${_install_prefix} \
    --with-mpfr=${_install_prefix} \
    --disable-shared \
    --enable-static

  make
  make DESTDIR="$srcdir/dest" install
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package_opt-mpc-static() {
  mkdir -p ${pkgdir}${_install_prefix}
  cp -rf $srcdir/dest${_install_prefix}/include ${pkgdir}${_install_prefix}/
  cp -rf $srcdir/dest${_install_prefix}/lib ${pkgdir}${_install_prefix}/
  cp -rf $srcdir/dest${_install_prefix}/share ${pkgdir}${_install_prefix}/

  rm -f ${pkgdir}${_install_prefix}/share/info/dir
}
