# Contributor: Alexey Pavlov <Alexpux@gmail.com>

_install_prefix="/opt${MSYSTEM_PREFIX}"

_enable_gcc_bootmode=yes

_realname='mpfr'
pkgbase=opt-${_realname}-static
pkgname=('opt-mpfr-static')
_pkgver=4.2.2
pkgver=${_pkgver}
# pkgver=${_pkgver}.p1
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
groups=('libraries' 'development')
options=('staticlibs')
url="https://www.mpfr.org/"
msys2_references=(
  "cpe: cpe:/a:mpfr:gnu_mpfr"
)
license=('spdx:LGPL-3.0-or-later')
makedepends=('opt-gmp-static>=5.0' 'autotools' 'gcc' 'autoconf-archive')
source=(https://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc})
        # patches.patch)
# wget https://www.mpfr.org/mpfr-4.2.1/allpatches -O patches.patch
validpgpkeys=('A534BE3F83E241D918280AEB5831D11A0D4DB02A') # Vincent Lefevre <vincent@vinc17.net>
sha256sums=('b67ba0383ef7e8a8563734e2e889ef5ec3c3b898a01d00fa0a6869ad81c6ce01'
            'SKIP')

prepare() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  # patch -p1 -i ${srcdir}/patches.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${_realname}-${_pkgver}"

  if [[ "$_enable_gcc_bootmode" != "yes" ]]; then
    export PATH="/opt/usr/bin:$PATH"
    export CC=/opt/usr/bin/gcc.exe
    export CXX=/opt/usr/bin/g++.exe
    export RANLIB=/opt/usr/bin/ranlib.exe
    export LD=/opt/usr/bin/ld.exe
  fi

  ./configure --build=${CHOST} \
    --prefix=${_install_prefix} \
    --with-gmp=${_install_prefix} \
    --disable-shared \
    --enable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${_realname}-${_pkgver}"
  make check
}

package_opt-mpfr-static() {
  mkdir -p ${pkgdir}${_install_prefix}
  cp -rf ${srcdir}/dest${_install_prefix}/include ${pkgdir}${_install_prefix}/
  cp -rf ${srcdir}/dest${_install_prefix}/lib ${pkgdir}${_install_prefix}/
  cp -rf ${srcdir}/dest${_install_prefix}/share ${pkgdir}${_install_prefix}/

  rm -f ${pkgdir}${_install_prefix}/share/info/dir
}
